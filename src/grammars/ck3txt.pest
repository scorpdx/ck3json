WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

identifier = @{ tag }
tag = @{ (ASCII_ALPHANUMERIC | "_" | "-" | ".")+ }

object = {
    "{" ~ "}" |
    "{" ~ pair* ~ "}"
}

pair = { identifier ~ "=" ~ value }

array = {
    "{" ~ "}" |
    "{" ~ (array_pair | value)* ~ "}"
}
array_pair = { pair }

value = _{ object | array | rgb | date | string | number | boolean | tag }

boolean = @{ ("yes" | "no") ~ !ASCII_ALPHA }

string = ${ "\"" ~ string_inner ~ "\"" }
string_inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{ float | int }

int = @{"-"? ~ ASCII_DIGIT+}
float = @{int? ~ "." ~ ASCII_DIGIT+ ~ "f"?}
rgb = {"rgb" ~ array}
date = ${ ("\"" ~ date_inner ~ "\"") | date_inner }
date_inner = @{ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }

save_id = { "SAV" ~ ASCII_ALPHANUMERIC{20} }
body = { pair* }

file = _{
    SOI ~
    save_id? ~
    body ~
    EOI
}